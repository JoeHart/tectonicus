plugins {
	id 'application'
	id 'org.ajoberstar.grgit' version '4.0.2'
	id 'com.github.johnrengelman.shadow' version '6.0.0'
}

archivesBaseName = 'Tectonicus'
project.version = '2.26.03'
sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = "tectonicus.TectonicusApp"

compileJava {
	options.debug = false
}

repositories {
	jcenter()
}

ext {
	lwjglVersion = '3.2.3'
	picocliVersion = '4.5.0'
	lombokVersion = '1.18.12'
	junitVersion = '5.6.2'
}

dependencies {
	implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
	implementation("org.lwjgl:lwjgl",
				   "org.lwjgl:lwjgl-opengl",
				   "org.lwjgl:lwjgl-glfw",
				   'org.apache.commons:commons-text:1.9',
				   'org.joml:joml:1.9.22',
				   'xyz.nickr:nbt:1.1.1',
				   "info.picocli:picocli:$picocliVersion@jar",
				   "org.jogamp.jogl:jogl-all:2.3.2",
				   "com.fasterxml.jackson.core:jackson-databind:2.11.2",
				   "org.apache.logging.log4j:log4j-core:2.13.3")
	implementation files ('Libs/Jars/jnbt.jar', 'Libs/Jars/jpct.jar', 'Libs/Jars/ProcessingCore.jar', 'Libs/Jars/BiomeExtractor.jar')
	runtimeOnly("org.lwjgl:lwjgl::natives-windows",
				"org.lwjgl:lwjgl-glfw::natives-windows",
				"org.lwjgl:lwjgl-opengl::natives-windows",
				"org.lwjgl:lwjgl::natives-macos",
				"org.lwjgl:lwjgl-glfw::natives-macos",
				"org.lwjgl:lwjgl-opengl::natives-macos",
				"org.lwjgl:lwjgl::natives-linux",
				"org.lwjgl:lwjgl-glfw::natives-linux",
				"org.lwjgl:lwjgl-opengl::natives-linux")

	compileOnly "org.projectlombok:lombok:$lombokVersion"
	annotationProcessor("org.projectlombok:lombok:$lombokVersion",
						"info.picocli:picocli-codegen:$picocliVersion")
	
	testImplementation("org.junit.jupiter:junit-jupiter-api:$junitVersion",
					   'org.hamcrest:hamcrest:2.2')
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
	testCompileOnly "org.projectlombok:lombok:$lombokVersion"
	testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"
}

shadowJar {
	doFirst {
		def props = new Properties()
		props.setProperty('buildDate', new Date().format('MMMM dd yyyy'))
		props.setProperty('buildTime', new Date().format('hhmm'))
		props.setProperty('buildNumber', grgit.log().size().toString())
		props.setProperty('version', "${project.version}")
		def writer = new FileWriter(file('build/resources/main/tectonicus.buildInfo'))
		props.store(writer, 'Tectonicus build info')
		writer.flush()
		writer.close()
	}

	archiveClassifier = ''

	exclude ('processing/**', 'com/threed/**')
	minimize {
		exclude(dependency('xyz.nickr:nbt:.*'))
	}
}

distributions {
	shadow {
		distributionBaseName = archivesBaseName
		contents {
			from files('Data/defaultBlockConfig.xml')
			from('Docs') {
				include('*.xml', '*.txt')
				exclude('notes.txt')
			}
		}
	}
}

test {
    useJUnitPlatform()
}
